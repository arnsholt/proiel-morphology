!Multichar_Symbols +Noun +Adj +Adv +Nom +Voc +Acc +Gen +Dat +Abl +Sg +Pl +Masc +Fem +Neut +Pos +Comp +Super +Verb +Part +Pres +Perf +Fut
!    +Verb +Dep
!    +1S +2S +3S +1P +2P +3P ! Persons
!    +Pres +Fut +Impf ! Tempora
!    +Ind +Impv +Subj ! Modi
!    +Inf
!    +Act +Pass ! Voices
Multichar_Symbols
    +Noun +Adj +Adv +Verb ! Word classes
    +Nom +Voc +Acc +Gen +Dat +Abl ! Cases
    +Sg +Pl ! Numbers
    +Masc +Fem +Neut ! Genders
    +Pos +Comp +Super ! Comparison grades
    +Part +Dep ! Misc. verb stuff
    +1S +2S +3S +1P +2P +3P ! Persons
    +Pres +Perf +Fut +Impf ! Tempora
    +Ind +Impv +Subj +Inf ! Modi
    +Act +Pass ! Voices

! Noun lexicon
LEXICON Root
    Decl1 ;
    Decl2 ;
    Decl3 ;
    Decl4 ;
    Decl5 ;
    Adj ;
    Participles ;

! First declination: rosa.
LEXICON Decl1
rosa+Noun:ros D1 ;

LEXICON D1
    D1Endings ;

LEXICON D1Endings
+Nom+Sg:a # ;
+Voc+Sg:a # ;
+Acc+Sg:am # ;
+Gen+Sg:ae # ;
+Dat+Sg:ae # ;
+Abl+Sg:a # ;

+Nom+Pl:ae # ;
+Voc+Pl:ae # ;
+Acc+Pl:as # ;
+Gen+Pl:arum # ;
+Dat+Pl:is # ;
+Abl+Pl:is # ;

! Second declination: dominus, puer, bellum.
! BUG: Not all nouns on -er drop the e before ending. Some, like numerus, keep
! XXX: Nouns on -ius have -i in the vocative, not -e.
! it. Ref. Ernout p. 26.
LEXICON Decl2
dominus+Noun:domin D2M ;
numerus+Noun:numer D2M ;
filius+Noun:fili D2M-ius ;
puer+Noun:puer D2-er ;
ager+Noun:agEr D2-er ;
bellum+Noun:bell D2N ;

LEXICON D2M
    D2EndingsM ;
    D2EndingsCommon ;

LEXICON D2N
    D2EndingsN ;
    D2EndingsCommon ;

LEXICON D2-er
+Nom+Sg:0 # ;
+Voc+Sg:0 # ;
    D2EndingsCommonM ;
    D2EndingsCommon ;

LEXICON D2M-ius
+Nom+Sg:-us # ; ! XXX: Duplicate from D2EndingsM. Refactor?
+Voc+Sg:0 # ;
+Gen+Sg:0 # ;
    D2EndingsCommonM ;
    D2EndingsCommon ;

LEXICON D2EndingsM
+Nom+Sg:-us # ;
+Voc+Sg:-e # ;
    D2EndingsCommonM ;

LEXICON D2EndingsCommonM
+Nom+Pl:-i # ;
+Voc+Pl:-i # ;
+Acc+Pl:-os # ;

LEXICON D2EndingsN
+Nom+Sg:-um # ;
+Voc+Sg:-um # ;

+Nom+Pl:-a # ;
+Voc+Pl:-a # ;
+Acc+Pl:-a # ;

LEXICON D2EndingsCommon
+Acc+Sg:-um # ;
+Gen+Sg:-i # ;
+Dat+Sg:-o # ;
+Abl+Sg:-o # ;

+Gen+Pl:-orum # ;
+Dat+Pl:-is # ;
+Abl+Pl:-is # ;

! TODO: ``3B'' declination (gen.pl. -ium)
LEXICON Decl3
rex+Noun:rex D3EndingsNoneMF ;
rex+Noun:reg D3EndingsMF ;

corpus+Noun:corpus D3EndingsNoneN ;
corpus+Noun:corpor D3EndingsN ;

LEXICON D3EndingsNoneMF
+Nom+Sg:0 # ;
+Voc+Sg:0 # ;

LEXICON D3EndingsMF
    D3EndingsCommon ;
+Acc+Sg:em # ;

+Nom+Pl:es # ;
+Voc+Pl:es # ;
+Acc+Pl:es # ;

LEXICON D3EndingsNoneN
    D3EndingsNoneMF ;
+Acc+Sg:0 # ;

LEXICON D3EndingsN
    D3EndingsCommon ;

+Nom+Pl:a # ;
+Voc+Pl:a # ;
+Acc+Pl:a # ;

LEXICON D3EndingsCommon
+Gen+Sg:is # ;
+Dat+Sg:i # ;
+Abl+Sg:e # ;

+Gen+Pl:um # ;
+Dat+Pl:ibus # ;
+Abl+Pl:ibus # ;

! TODO: cornus (neuter D4 nouns)
LEXICON Decl4
fructus+Noun:fruct D4EndingsMF ;
cornus+Noun:corn D4EndingsN ;

LEXICON D4EndingsMF
+Acc+Sg:um # ;

+Nom+Pl:us # ;
+Voc+Pl:us # ;
+Acc+Pl:us # ;
    D4EndingsCommon ;

LEXICON D4EndingsN
+Acc+Sg:us # ;

+Nom+Pl:ua # ;
+Voc+Pl:ua # ;
+Acc+Pl:ua # ;
    D4EndingsCommon ;

LEXICON D4EndingsCommon
+Nom+Sg:us # ;
+Voc+Sg:us # ;
+Gen+Sg:us # ;
+Dat+Sg:ui # ;
+Abl+Sg:u # ;

+Gen+Pl:uum # ;
+Dat+Pl:ibus # ;
+Abl+Pl:ibus # ;

LEXICON Decl5
res+Noun:re D5Endings ;

LEXICON D5Endings
+Nom+Sg:s # ;
+Voc+Sg:s # ;
+Acc+Sg:m # ;
+Gen+Sg:i # ;
+Dat+Sg:i # ;
+Abl+Sg:0 # ;

+Nom+Pl:s # ;
+Voc+Pl:s # ;
+Acc+Pl:s # ;
+Gen+Pl:rum # ;
+Dat+Pl:bus # ;
+Abl+Pl:bus # ;

! Adjective lexicon.
! XXX: To save me the trouble of refactoring the +Adj in the adjective code
! in a reasonably sensible way, we do Adverb derivation by adding the +Adv tag
! after the degree. A substitution is composed into the lexicon in
! build/nouns.fst.build which converts from $root+Adj+$grade+Adv to
! $root+Adv+$grade. It's a bit of a hack, but it's a great effort-saver. =)
LEXICON Adj
longus+Adj:long AdjStd ;
acer+Adj:acEr AdjCons3 ;
brevis+Adj:brev AdjCons2 ;
felix+Adj:felix AdjCons1None ;
felix+Adj:felic AdjCons1 ;

! ``Normal'' (that is, thematic) adjectives.
LEXICON AdjStd
+Pos:0 AdjStdPos ;
    AdjComp ;
    AdjSuper ;

LEXICON AdjStdPos
+Masc:0 D2M ;
+Fem:0 D1 ;
+Neut:0 D2N ;
+Adv:e # ;

LEXICON AdjCons3
+Pos+Masc:0 AdjCons23EndingsM ;
+Pos+Fem:0 AdjCons23EndingsF ;
+Pos+Neut:0 AdjCons3EndingsN ;
    AdjComp ;
+Super:rim AdjStdPos ; ! No - to indicate morpheme boundary, so that the e isn't dropped.
    AdjConsAdv ;

! Consonant root adjectives with two forms in the nom.sg.
! XXX: I think this works. And even better, it's diachronically correct, if I read Ernout correctly.
LEXICON AdjCons2
+Pos+Masc:0 AdjCons23EndingsF ; ! Conveniently enough the feminine endings in the 3-form adjectives are the mf endings in the 2-form ones. This fits diachronically as well.
+Pos+Fem:0 AdjCons23EndingsF ;
+Pos+Neut:0 AdjCons3EndingsN ;
    AdjComp ;
    AdjSuper ;
    AdjConsAdv ;

! Consonant root adjectives with a single form in the nom.sg.
LEXICON AdjCons1None
+Pos+Masc:0 AdjCons1NoneMF ;
+Pos+Fem:0 AdjCons1NoneMF ;
+Pos+Neut:0 AdjCons1NoneN ;

LEXICON AdjCons1NoneMF
+Nom+Sg:0 # ;
+Voc+Sg:0 # ;

LEXICON AdjCons1NoneN
    AdjCons1NoneMF ;
+Acc+Sg:0 # ;

LEXICON AdjCons1
+Pos:0 AdjCons1Pos ;
    AdjComp ;
    AdjSuper ;
    AdjConsAdv ;

LEXICON AdjCons1Pos
+Masc:0 AdjConsEndingsMF ;
+Fem:0 AdjConsEndingsMF ;
+Neut:0 AdjConsEndingsN ;

! XXX: These endings are mostly duplicated from the D3 lexicons above. Try to
! factor them together? Unfortunately, that means a bit of weird
! special-casing, since the cases that need to be changed are abl.sg.,
! gen.pl., and nom.voc.acc.pl.neut. Might be easier to do if I implemented the
! securis and urbs declinations, which follow a similar pattern...
LEXICON AdjCons23EndingsM
+Nom+Sg:0 # ;
+Voc+Sg:0 # ;
    AdjConsEndingsMF ;

LEXICON AdjCons23EndingsF
+Nom+Sg:-is # ;
+Voc+Sg:-is # ;
    AdjConsEndingsMF ;

LEXICON AdjConsEndingsMF
+Acc+Sg:-em # ;

+Nom+Pl:-es # ;
+Voc+Pl:-es # ;
+Acc+Pl:-es # ;
    AdjConsEndingsCommon ;

LEXICON AdjCons3EndingsN
+Nom+Sg:-e # ;
+Voc+Sg:-e # ;
+Acc+Sg:-e # ;
    AdjConsEndingsN ;

LEXICON AdjConsEndingsN
+Nom+Pl:-ia # ;
+Voc+Pl:-ia # ;
+Acc+Pl:-ia # ;
    AdjConsEndingsCommon ;

LEXICON AdjConsEndingsCommon
+Gen+Sg:-is # ;
+Dat+Sg:-i # ;
+Abl+Sg:-i # ;

+Gen+Pl:-ium # ;
+Dat+Pl:-ibus # ;
+Abl+Pl:-ibus # ;

LEXICON AdjConsAdv
+Pos+Adv:-iter # ;

! Comparatives of adjectives.
LEXICON AdjComp
+Comp+Masc:-ior AdjCompMF ;
+Comp+Fem:-ior AdjCompMF ;
+Comp+Neut:-ius D3EndingsNoneN ;
+Comp+Neut:-ior D3EndingsN ;
+Comp+Adv:-ius # ;

LEXICON AdjCompMF
    D3EndingsNoneMF ;
    D3EndingsMF ;

! Superlatives of adjectives.
LEXICON AdjSuper
+Super:issim AdjStdPos ;

! Participles and other deverbal nominals.
LEXICON Participles
amo+Verb:aman PresStemPart ;
amo+Verb:amat SupinePart;
hortor+Verb+Dep:hortan PresStemPart ;
hortor+Verb+Dep:hortat SupinePart ;

moneo+Verb:monen PresStemPart ;
moneo+Verb:monit SupinePart ;
vereor+Verb+Dep:veren PresStemPart ;
vereor+Verb+Dep:verit SupinePart ;

rego+Verb:regen PresStemPart ;
rego+Verb:rect SupinePart ;
sequor+Verb+Dep:sequen PresStemPart ;
sequor+Verb+Dep:secut SupinePart ;
capio+Verb:capien PresStemPart ;
capio+Verb:capt SupinePart ;
patior+Verb+Dep:patien PresStemPart ;
patior+Verb+Dep:pass SupinePart ;

audio+Verb:audien PresStemPart ;
audio+Verb:audit SupinePart ;
partior+Verb+Dep:partien PresStemPart ;
partior+Verb+Dep:partit SupinePart ;

LEXICON PresStemPart
+Part+Pres:0 PresPart ;
+Ger:d Gerund ;
+Gerv:d AdjStdPos ;

LEXICON PresPart
+Masc+Nom+Sg:s # ;
+Masc+Voc+Sg:s # ;
+Fem+Nom+Sg:s # ;
+Fem+Voc+Sg:s # ;
+Neut+Nom+Sg:s # ;
+Neut+Voc+Sg:s # ;
+Neut+Acc+Sg:s # ;
0:t AdjCons1Pos ;
+Masc+Abl+Sg:te # ;
+Fem+Abl+Sg:te # ;
+Neut+Abl+Sg:te # ;

LEXICON Gerund
+Acc:um # ;
+Gen:i # ;
+Dat:o # ;
+Abl:o # ;

LEXICON SupinePart
+Part+Perf:0 AdjStdPos ;
+Part+Fut:ur AdjStdPos ;
+SupI:um # ;
+SupII:u # ;
