Multichar_Symbols +Noun +Adj +Nom +Voc +Acc +Gen +Dat +Abl +Sg +Pl

! Noun lexicon
LEXICON Root
    Decl1 ;
    Decl2 ;
    Decl3 ;
    Decl4 ;
    Decl5 ;
    Adj ;

LEXICON Decl1
rosa+Noun:ros D1 ;

LEXICON D1
    D1Endings ;

LEXICON D1Endings
+Nom+Sg:a # ;
+Voc+Sg:a # ;
+Acc+Sg:m # ;
+Gen+Sg:ae # ;
+Dat+Sg:ae # ;
+Abl+Sg:a # ;

+Nom+Pl:ae # ;
+Voc+Pl:ae # ;
+Acc+Pl:as # ;
+Gen+Pl:arum # ;
+Dat+Pl:is # ;
+Abl+Pl:is # ;

! For nouns a la ager, agri, we might want to bring in twolc that remove the e
! before endings, so that we can handle them like other D2 nouns...
LEXICON Decl2
dominus+Noun:domin D2M ;
bellum+Noun:bell D2N ;

LEXICON D2M
    D2EndingsM ;
    D2EndingsCommon ;

LEXICON D2N
    D2EndingsN ;
    D2EndingsCommon ;

LEXICON D2EndingsM
+Nom+Sg:us # ;
+Voc+Sg:e # ;

+Nom+Pl:i # ;
+Voc+Pl:i # ;
+Acc+Pl:os # ;

LEXICON D2EndingsN
+Nom+Sg:um # ;
+Voc+Sg:um # ;

+Nom+Pl:a # ;
+Voc+Pl:a # ;
+Acc+Pl:a # ;

LEXICON D2EndingsCommon
+Acc+Sg:um # ;
+Gen+Sg:i # ;
+Dat+Sg:o # ;
+Abl+Sg:o # ;

+Gen+Pl:orum # ;
+Dat+Pl:is # ;
+Abl+Pl:is # ;

! TODO: ``3B'' declination (gen.pl. -ium)
LEXICON Decl3
rex+Noun:rex D3EndingsNoneMF ;
rex+Noun:reg D3EndingsMF ;

corpus+Noun:corpus D3EndingsNoneN ;
corpus+Noun:corpor D3EndingsN ;

LEXICON D3EndingsNoneMF
+Nom+Sg:0 # ;
+Voc+Sg:0 # ;

LEXICON D3EndingsMF
    D3EndingsCommon ;
+Acc+Sg:em # ;

+Nom+Pl:es # ;
+Voc+Pl:es # ;
+Acc+Pl:es # ;

LEXICON D3EndingsNoneN
+Nom+Sg:0 # ;
+Voc+Sg:0 # ;
+Acc+Sg:0 # ;

LEXICON D3EndingsN
    D3EndingsCommon ;

+Nom+Pl:a # ;
+Voc+Pl:a # ;
+Acc+Pl:a # ;

LEXICON D3EndingsCommon
+Gen+Sg:is # ;
+Dat+Sg:i # ;
+Abl+Sg:e # ;

+Gen+Pl:um # ;
+Dat+Pl:ibus # ;
+Abl+Pl:ibus # ;

LEXICON Decl4
fructus+Noun:fruct D4Endings ;

LEXICON D4Endings
+Nom+Sg:us # ;
+Voc+Sg:us # ;
+Acc+Sg:um # ;
+Gen+Sg:us # ;
+Dat+Sg:ui # ;
+Abl+Sg:u # ;

+Nom+Pl:us # ;
+Voc+Pl:us # ;
+Acc+Pl:us # ;
+Gen+Pl:uum # ;
+Dat+Pl:ibus # ;
+Abl+Pl:ibus # ;

LEXICON Decl5
res+Noun:re D5Endings ;

LEXICON D5Endings
+Nom+Sg:s # ;
+Voc+Sg:s # ;
+Acc+Sg:m # ;
+Gen+Sg:i # ;
+Dat+Sg:i # ;
+Abl+Sg:0 # ;

+Nom+Pl:s # ;
+Voc+Pl:s # ;
+Acc+Pl:s # ;
+Gen+Pl:rum # ;
+Dat+Pl:bus # ;
+Abl+Pl:bus # ;

! TODO: D3 adjectives.
LEXICON Adj
longus+Adj:long AdjStd ;

LEXICON AdjStd
+Pos:0 AdjStdPos ;
    AdjComp ;
    AdjSuper ;

LEXICON AdjStdPos
+Masc:0 D2M ;
+Fem:0 D1 ;
+Neut:0 D2N ;

LEXICON AdjComp
+Comp+Masc:ior AdjCompMF ;
+Comp+Fem:ior AdjCompMF ;
+Comp+Neut:ius D3EndingsNoneN ;
+Comp+Neut:ior D3EndingsN ;

LEXICON AdjCompMF
    D3EndingsNoneMF ;
    D3EndingsMF ;

LEXICON AdjSuper
+Super:issim AdjStdPos ;
