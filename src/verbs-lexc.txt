!Multichar_Symbols +Noun +Adj +Adv +Nom +Voc +Acc +Gen +Dat +Abl +Sg +Pl +Masc +Fem +Neut +Pos +Comp +Super +Verb +Part +Pres +Perf +Fut
!    +Verb +Dep
!    +1st+Sg +2nd+Sg +3rd+Sg +1st+Pl +2nd+Pl +3rd+Pl ! Persons
!    +Pres +Fut +Impf ! Tempora
!    +Ind +Impv +Subj ! Modi
!    +Inf
!    +Act +Pass ! Voices
Multichar_Symbols
    +Noun +Adj +Adv +Verb ! Word classes
    +Nom +Voc +Acc +Gen +Dat +Abl ! Cases
    +Sg +Pl ! Numbers
    +Masc +Fem +Neut ! Genders
    +Pos +Comp +Super ! Comparison grades
    +Part +Dep ! Misc. verb stuff
    +1st +2nd +3rd ! Persons
    +Pres +Perf +Fut +Impf ! Tempora
    +Ind +Impv +Subj +Inf ! Modi
    +Act +Pass ! Voices

! Verb lexicon
LEXICON Root
! Different categories for each conjugation. Ultimately they may share parts,
! but I think this is a good starting point.
    Conj1 ;
    Conj2 ;
    Conj3 ;
    Conj4 ;
    Sum ;

LEXICON Conj1
amo+Verb:ama C1Pres ;
amo+Verb:amav C14PerfSystem ;
hortor+Verb+Dep:horta C1PresDep ;

! 1st conjugation present system
LEXICON C1Pres
    C14Pres ;
    C1PresSubj ;
    C12Fut ;
    C12Impf ;
    C14ImpfSubj ;
    CNon3Impv ;
    CNon3InfPres ;

LEXICON C1PresDep
    C14PresPass ;
    C1PresSubjPass ;
    C12FutPass ;
    C12ImpfPass ;
    C14ImpfSubjPass ;
    C14ImpvPass ;
    CNon3InfPresPass ;

LEXICON C1PresSubj
    C1PresSubjAct ;
    C1PresSubjPass ;

LEXICON C1PresSubjAct
+Pres+Subj:-e EndingsNonPresAct ;

LEXICON C1PresSubjPass
+Pres+Subj:-e EndingsNonPresPass ;

LEXICON Conj2
moneo+Verb:mone C2Pres ;
moneo+Verb:monu C14PerfSystem ;
vereor+Verb+Dep:vere C2PresDep ;

LEXICON C2Pres
    C14Pres ;
    C24PresSubj ;
    C12Fut ;
    C12Impf ;
    C14ImpfSubj ;
    CNon3Impv ;
    CNon3InfPres ;

LEXICON C2PresDep
    C14PresPass ;
    C24PresSubjPass ;
    C12FutPass ;
    C12ImpfPass ;
    C14ImpfSubjPass ;
    C14ImpvPass ;
    CNon3InfPresPass ;

LEXICON Conj3
rego+Verb:reg C3Pres ;
rego+Verb:rex C14PerfSystem ;
capio+Verb:capI C3Pres ;
capio+Verb:cep C14PerfSystem ;
sequor+Verb+Dep:sequ C3PresDep ;
patior+Verb+Dep:patI C3PresDep ;

LEXICON C3Pres
    C14Pres ;
    C24PresSubj ;
    C34Fut ;
    C34Impf ;
    C14ImpfSubj ;
    C3Impv ;
    C3InfPres ;

LEXICON C3PresDep
    C14PresPass ;
    C24PresSubjPass ;
    C34FutPass ;
    C34ImpfPass ;
    C14ImpfSubjPass ;
    C14ImpvPass ;
    C3InfPresPass ;

LEXICON Conj4
audio+Verb:audi C4Pres ;
audio+Verb:audiv C14PerfSystem ;
partior+Verb+Dep:parti C4PresDep ;

LEXICON C4Pres
    C14Pres ;
    C24PresSubj ;
    C34Fut ;
    C34Impf ;
    C14ImpfSubj ;
    CNon3Impv ;
    CNon3InfPres ;

LEXICON C4PresDep
    C14PresPass ;
    C24PresSubjPass ;
    C34FutPass ;
    C34ImpfPass ;
    C14ImpfSubjPass ;
    C14ImpvPass ;
    CNon3InfPresPass ;

LEXICON Sum
sum+Verb+Pres+Ind:      SumPres ;
sum+Verb+Pres+Subj:si   EndingsNonPresAct ;
sum+Verb+Fut+Ind:er     EndingsPresIndAct ;
sum+Verb+Impf+Ind:era   EndingsNonPresAct ;
sum+Verb+Impf+Subj:esse EndingsNonPresAct ;
sum+Verb:fu             C14PerfSystem ;
sum+Verb:               SumImpv ;

sum+Verb+Inf+Pres+Act:esse # ;
sum+Verb+Inf+Fut+Act:fore # ;

LEXICON SumPres
+1st+Sg+Act:sum   # ;
+2nd+Sg+Act:es    # ;
+3rd+Sg+Act:est   # ;
+1st+Pl+Act:sumus # ;
+2nd+Pl+Act:estis # ;
+3rd+Pl+Act:sunt  # ;

LEXICON SumImpv
+Pres+Impv+2nd+Sg+Act:es   # ;
+Pres+Impv+2nd+Pl+Act:este # ;

+Fut+Impv+2nd+Sg+Act:esto   # ;
+Fut+Impv+3rd+Sg+Act:esto   # ;
+Fut+Impv+2nd+Pl+Act:estote # ;
+Fut+Impv+3rd+Pl+Act:sunto  # ;

! Regular present indicative, first to fourth conjugations.
LEXICON C14Pres
    C14PresAct ;
    C14PresPass ;

LEXICON C14PresAct
+Pres+Ind:0 EndingsPresIndAct ;

LEXICON C14PresPass
+Pres+Ind:0 EndingsPresIndPass ;

! Regular present subjunctive, second to fourth conjugations.
LEXICON C24PresSubj
    C24PresSubjAct ;
    C24PresSubjPass ;

LEXICON C24PresSubjAct
+Pres+Subj:-a EndingsNonPresAct ;

LEXICON C24PresSubjPass
+Pres+Subj:-a EndingsNonPresPass ;

! Future indicative, first & second conjugations.
LEXICON C12Fut
    C12FutAct ;
    C12FutPass ;

LEXICON C12FutAct
+Fut+Ind:-b EndingsPresIndAct ;

LEXICON C12FutPass
+Fut+Ind:-b EndingsPresIndPass ;

! Future indicative, third & fourth conjugations.
LEXICON C34Fut
    C34FutAct ;
    C34FutPass ;

LEXICON C34FutAct
+Fut+Ind:-a EndingsNonPres1SAct ;
+Fut+Ind:-e EndingsCommonAct ;

LEXICON C34FutPass
+Fut+Ind:-a EndingsNonPres1SPass ;
+Fut+Ind:-e EndingsCommonPass ;

! Imperfect indicative tense, first and second conjugations.
LEXICON C12Impf
    C12ImpfAct ;
    C12ImpfPass ;

LEXICON C12ImpfAct
+Impf+Ind:-ba EndingsNonPresAct ;

LEXICON C12ImpfPass
+Impf+Ind:-ba EndingsNonPresPass ;

! XXX: It might be possible to do the imperfect indicative with just C12Impf
!      and insert the -e in verbs-twolc.txt. But is it worth it?
! Imperfect indicative tense, third and fourth conjugations.
LEXICON C34Impf
    C34ImpfAct ;
    C34ImpfPass ;

LEXICON C34ImpfAct
+Impf+Ind:-eba EndingsNonPresAct ;

LEXICON C34ImpfPass
+Impf+Ind:-eba EndingsNonPresPass ;

! Imperfect subjunctive tense, all conjugations.
LEXICON C14ImpfSubj
    C14ImpfSubjAct ;
    C14ImpfSubjPass ;

LEXICON C14ImpfSubjAct
+Impf+Subj:-re EndingsNonPresAct ;

LEXICON C14ImpfSubjPass
+Impf+Subj:-re EndingsNonPresPass ;

! Imperative paradigms.
LEXICON CNon3Impv
+Pres+Impv+2nd+Sg+Act:0 # ;
    CommonImpv ;

LEXICON C3Impv
+Pres+Impv+2nd+Sg+Act:-e # ;
    CommonImpv ;

! Imperative forms common to al conjugations.
LEXICON CommonImpv
+Pres+Impv+2nd+Pl+Act:-te # ;
    C14ImpvPass ;

+Fut+Impv+2nd+Sg+Act:-to # ;
+Fut+Impv+3rd+Sg+Act:-to # ;
+Fut+Impv+2nd+Pl+Act:-tote # ;
+Fut+Impv+3rd+Pl+Act:-nto # ;

! Passive imperatives. Factored out to make room for deponents.
LEXICON C14ImpvPass
+Pres+Impv+2nd+Sg+Pass:-re # ;
+Pres+Impv+2nd+Pl+Pass:-mini # ;

! Present infinitives.
LEXICON CNon3InfPres
    C14InfPresAct ;
    CNon3InfPresPass ;

LEXICON C3InfPres
    C14InfPresAct ;
    C3InfPresPass ;

LEXICON C14InfPresAct
+Inf+Pres+Act:-re # ;

LEXICON CNon3InfPresPass
+Inf+Pres+Pass:-ri # ;

LEXICON C3InfPresPass
+Inf+Pres+Pass:-i # ;

! Regular perfect system, all conjugations.
LEXICON C14PerfSystem
    C14Perf ;
    C14PerfSubj ;
    C14FutII ;
    C14Pqp ;
    C14PqpSubj ;
    C14InfPerf ;

! Perfect indicative tense.
LEXICON C14Perf
+Perf+Ind:0 EndingsPerf ;

! Perfect subjunctive tense.
LEXICON C14PerfSubj
+Perf+Subj:-eri EndingsNonPresAct ;

! Future perfect tense.
LEXICON C14FutII
+FutII+Ind+1st+Sg+Act:-er-o # ; ! Ludicrously special-cased to avoid too fragmented code.
+FutII+Ind:-eri EndingsCommonAct ;

! Pluperfect indicative tense.
LEXICON C14Pqp
+Pqp+Ind:-era EndingsNonPresAct ;

! Pluperfect subjunctive tense.
LEXICON C14PqpSubj
+Pqp+Subj:-isse EndingsNonPresAct ;

! Perfect infinitive.
LEXICON C14InfPerf
+Inf+Perf+Act:-isse # ;

! Endings for the present indicative.
LEXICON EndingsPresIndAct
+1st+Sg+Act:-o # ;
    EndingsCommonAct ;

LEXICON EndingsPresIndPass
+1st+Sg+Pass:-or # ;
    EndingsCommonPass ;

! Endings for non-present indicative tenses.
LEXICON EndingsNonPresAct
    EndingsNonPres1SAct ;
    EndingsCommonAct ;

LEXICON EndingsNonPresPass
    EndingsNonPres1SPass ;
    EndingsCommonPass ;

! Endings for the first person singular of non-present indicative tenses.
LEXICON EndingsNonPres1SAct
+1st+Sg+Act:-m # ;

LEXICON EndingsNonPres1SPass
+1st+Sg+Pass:-r # ;

! Endings common to most forms in the system.
LEXICON EndingsCommonAct
+2nd+Sg+Act:-s # ;
+3rd+Sg+Act:-t # ;
+1st+Pl+Act:-mus # ;
+2nd+Pl+Act:-tis # ;
+3rd+Pl+Act:-nt # ;

LEXICON EndingsCommonPass
+2nd+Sg+Pass:-ris # ;
+2nd+Sg+Pass:-re # ;
+3rd+Sg+Pass:-tur # ;
+1st+Pl+Pass:-mur # ;
+2nd+Pl+Pass:-mini # ;
+3rd+Pl+Pass:-ntur # ;

! Perfect endings.
LEXICON EndingsPerf
+1st+Sg+Act:-i # ;
+2nd+Sg+Act:-isti # ;
+3rd+Sg+Act:-it # ;
+1st+Pl+Act:-imus # ;
+2nd+Pl+Act:-istis # ;
+3rd+Pl+Act:-erunt # ;
+3rd+Pl+Act:-ere # ;
