Multichar_Symbols

! Noun lexicon
LEXICON Root
    Decl1 ;
    Decl2 ;
    Decl3 ;
    Decl4 ;
    Decl5 ;
    Adj ;
    Participles ;

! First declination: rosa.
LEXICON Decl1
rosa+Noun+Fem:ros D1 ;

LEXICON D1
    D1Endings ;

LEXICON D1Endings
+Nom+Sg:-a # ;
+Voc+Sg:-a # ;
+Acc+Sg:-am # ;
+Gen+Sg:-ae # ;
+Dat+Sg:-ae # ;
+Abl+Sg:-a # ;

+Nom+Pl:-ae # ;
+Voc+Pl:-ae # ;
+Acc+Pl:-as # ;
+Gen+Pl:-arum # ;
+Dat+Pl:-is # ;
+Abl+Pl:-is # ;

! Second declination: dominus, puer, bellum.
! BUG: Not all nouns on -er drop the e before ending. Some, like numerus, keep
! it. Ref. Ernout p. 26.
! XXX: Nouns on -ius have -i in the vocative, not -e.
LEXICON Decl2
dominus+Noun+Masc:domin D2M ;
numerus+Noun+Masc:numer D2M ;
filius+Noun+Masc:fili D2M-ius ;
puer+Noun+Masc:puer D2-er ;
ager+Noun+Masc:agEr D2-er ;
bellum+Noun+Neut:bell D2N ;

LEXICON D2M
    D2EndingsM ;
    D2EndingsCommon ;

LEXICON D2N
    D2EndingsN ;
    D2EndingsCommon ;

LEXICON D2-er
+Nom+Sg:0 # ;
+Voc+Sg:0 # ;
    D2EndingsCommonM ;
    D2EndingsCommon ;

LEXICON D2M-ius
+Nom+Sg:-us # ; ! XXX: Duplicate from D2EndingsM. Refactor?
+Voc+Sg:0 # ;
+Gen+Sg:0 # ;
    D2EndingsCommonM ;
    D2EndingsCommon ;

LEXICON D2EndingsM
+Nom+Sg:-us # ;
+Voc+Sg:-e # ;
    D2EndingsCommonM ;

LEXICON D2EndingsCommonM
+Nom+Pl:-i # ;
+Voc+Pl:-i # ;
+Acc+Pl:-os # ;

LEXICON D2EndingsN
+Nom+Sg:-um # ;
+Voc+Sg:-um # ;

+Nom+Pl:-a # ;
+Voc+Pl:-a # ;
+Acc+Pl:-a # ;

LEXICON D2EndingsCommon
+Acc+Sg:-um # ;
+Gen+Sg:-i # ;
+Dat+Sg:-o # ;
+Abl+Sg:-o # ;

+Gen+Pl:-orum # ;
+Dat+Pl:-is # ;
+Abl+Pl:-is # ;

LEXICON Decl3
rex+Noun+Masc:rex D3EndingsNoneMF ;
rex+Noun+Masc:reg D3AEndingsMF ;

corpus+Noun+Neut:corpus D3EndingsNoneN ;
corpus+Noun+Neut:corpor D3AEndingsN ;

Caesar+Name+Masc:Caesar D3EndingsNoneMF ;
Caesar+Name+Masc:Caesar D3AEndingsMF ;

hostis+Noun+Masc:hostis D3EndingsNoneMF ;
hostis+Noun+Masc:host   D3BEndingsMF ;

mare+Noun+Neut:mare D3EndingsNoneN ;
mare+Noun+Neut:mar  D3BEndingsN ;

LEXICON D3EndingsNoneMF
+Nom+Sg:0 # ;
+Voc+Sg:0 # ;

LEXICON D3AEndingsMF
    D3AEndingsCommon ;
    D3EndingsCommonMF ;

LEXICON D3EndingsNoneN
    D3EndingsNoneMF ;
+Acc+Sg:0 # ;

LEXICON D3AEndingsN
+Nom+Pl:-a # ;
+Voc+Pl:-a # ;
+Acc+Pl:-a # ;
    D3AEndingsCommon ;
    D3EndingsCommon ;

LEXICON D3AEndingsCommon
+Abl+Sg:-e # ;
+Gen+Pl:-um # ;

LEXICON D3BEndingsMF
+Abl+Sg:-e # ;
    D3BEndingsCommon ;
    D3EndingsCommonMF ;

LEXICON D3BEndingsN
+Abl+Sg:-i # ;
+Nom+Pl:-ia # ;
+Voc+Pl:-ia # ;
+Acc+Pl:-ia # ;
    D3BEndingsCommon ;
    D3EndingsCommon ;

LEXICON D3BEndingsCommon
+Gen+Pl:-ium # ;

LEXICON D3EndingsCommonMF
+Acc+Sg:-em # ;

+Nom+Pl:-es # ;
+Voc+Pl:-es # ;
+Acc+Pl:-es # ;
    D3EndingsCommon ;

LEXICON D3EndingsCommon
+Gen+Sg:-is # ;
+Dat+Sg:-i # ;

+Dat+Pl:-ibus # ;
+Abl+Pl:-ibus # ;

LEXICON Decl4
fructus+Noun+Masc:fruct D4EndingsMF ;
cornu+Noun+Neut:corn D4EndingsN ;

LEXICON D4EndingsMF
+Nom+Sg:-us # ;
+Voc+Sg:-us # ;
+Acc+Sg:-um # ;

+Nom+Pl:-us # ;
+Voc+Pl:-us # ;
+Acc+Pl:-us # ;
    D4EndingsCommon ;

LEXICON D4EndingsN
+Nom+Sg:-u # ;
+Voc+Sg:-u # ;
+Acc+Sg:-u # ;

+Nom+Pl:-ua # ;
+Voc+Pl:-ua # ;
+Acc+Pl:-ua # ;
    D4EndingsCommon ;

LEXICON D4EndingsCommon
+Gen+Sg:-us # ;
+Dat+Sg:-ui # ;
+Abl+Sg:-u # ;

+Gen+Pl:-uum # ;
+Dat+Pl:-ibus # ;
+Abl+Pl:-ibus # ;

LEXICON Decl5
res+Noun+Fem:re D5Endings ;
dies+Noun+MF:die D5Endings ;

LEXICON D5Endings
+Nom+Sg:-s # ;
+Voc+Sg:-s # ;
+Acc+Sg:-m # ;
+Gen+Sg:-i # ;
+Dat+Sg:-i # ;
+Abl+Sg:0 # ;

+Nom+Pl:-s # ;
+Voc+Pl:-s # ;
+Acc+Pl:-s # ;
+Gen+Pl:-rum # ;
+Dat+Pl:-bus # ;
+Abl+Pl:-bus # ;

! Adjective lexicon.
LEXICON Adj
long:long AdjThematic ;
pulch:pulchEr AdjThematic-er ;
brev:brev AdjCons2 ;

meus+Pron+1st:me AdjThematicEndings ;
tuus+Pron+2nd:tu AdjThematicEndings ;
suus+Pron+3rd:su AdjThematicEndings ;
noster+Pron+1st:nostEr AdjThematicEndings-er ;
vester+Pron+2nd:vestEr AdjThematicEndings-er ;
! TODO:
!acer+Adj:acEr AdjCons3 ;
!felix+Adj:felix AdjCons1None ;
!felix+Adj:felic AdjCons1 ;

LEXICON AdjThematic
us+Adj:0      AdjThematic-Adj ;
e+Adv:0       AdvThematic ;

LEXICON AdjThematic-Adj
+Pos:0        AdjThematicEndings ;
+Comp:0       AdjComp ;
+Super:-issim AdjThematicEndings ;

LEXICON AdjThematic-er
er+Adj:0 AdjThematic-Adj-er ;
re+Adv:0 AdvThematic-er ;

LEXICON AdjThematic-Adj-er
+Pos:0 AdjThematicEndings-er ;
+Comp:0    AdjComp ;
+Super:rim AdjThematicEndings ; ! No morpheme boundary to suppress E-drop

LEXICON AdjCons2
is+Adj:0 AdjCons2-Adj ;
iter+Adv:0 AdvCons ;

LEXICON AdjCons2-Adj
+Pos:0        AdjCons2Pos ;
+Comp:0       AdjComp ;
+Super:-issim AdjThematicEndings ;

LEXICON AdjThematicEndings
+Masc+Nom+Sg:-us # ;
+Masc+Voc+Sg:-e  # ;
    AdjThematicEndingsCommon ;

LEXICON AdjThematicEndings-er
+Masc+Nom+Sg:0 # ;
+Masc+Voc+Sg:0 # ;
    AdjThematicEndingsCommon ;

LEXICON AdjComp
0:-ior AdjAthematicEndingsCommon ;
+MF+Nom+Sg:-ior-0   # ;
+MF+Voc+Sg:-ior-0   # ;
+MFN+Abl+Sg:-ior-e  # ;

+Neut+Nom+Sg:-ius # ;
+Neut+Voc+Sg:-ius # ;
+Neut+Acc+Sg:-ius # ;

+MFN+Gen+Pl:-ior-um # ;

+Neut+Nom+Pl:-ior-a # ;
+Neut+Voc+Pl:-ior-a # ;
+Neut+Acc+Pl:-ior-a # ;

LEXICON AdjThematicEndingsCommon
+MN+Acc+Sg:-um   # ;
+MN+Gen+Sg:-i    # ;
+MN+Dat+Sg:-o    # ;
+MN+Abl+Sg:-o    # ;

+Masc+Nom+Pl:-i  # ;
+Masc+Voc+Pl:-i  # ;
+Masc+Acc+Pl:-os # ;
+MN+Gen+Pl:-orum # ;
+MFN+Dat+Pl:-is  # ;
+MFN+Abl+Pl:-is  # ;

+Fem+Nom+Sg:-a  # ;
+Fem+Voc+Sg:-a  # ;
+Fem+Acc+Sg:-am # ;
+Fem+Gen+Sg:-ae # ;
+Fem+Dat+Sg:-ae # ;
+Fem+Abl+Sg:-a  # ;

+Fem+Nom+Pl:-ae   # ;
+Fem+Voc+Pl:-ae   # ;
+Fem+Acc+Pl:-as   # ;
+Fem+Gen+Pl:-arum # ;

+Neut+Nom+Sg:-um # ;
+Neut+Voc+Sg:-um # ;

+Neut+Nom+Pl:-a # ;
+Neut+Voc+Pl:-a # ;
+Neut+Acc+Pl:-a # ;

LEXICON AdjAthematicEndingsCommon
+MF+Acc+Sg:-em  # ;
+MFN+Gen+Sg:-is # ;
+MFN+Dat+Sg:-i  # ;

+MF+Nom+Pl:-es    # ;
+MF+Voc+Pl:-es    # ;
+MF+Acc+Pl:-es    # ;
+MFN+Dat+Pl:-ibus # ;
+MFN+Abl+Pl:-ibus # ;

LEXICON AdvThematic
+Pos:-e # ;
+Comp:-ius # ;
+Super:-issim-e # ;

LEXICON AdvThematic-er
+Pos:-e # ;
+Comp:-ius # ;
+Super:rime # ; ! No morpheme boundary to suppress E-drop

LEXICON AdvCons
+Pos:-iter # ;
+Comp:-ius # ;
+Super:-issim-e # ;

LEXICON AdjCons2Pos
+MF+Nom+Sg:-is # ;
+MF+Voc+Sg:-is # ;

+Neut+Nom+Sg:-e # ;
+Neut+Voc+Sg:-e # ;
+Neut+Acc+Sg:-e # ;

    AdjConsCommonEndings ;

LEXICON AdjCons1Pos
    AdjConsCommonEndings ;

LEXICON AdjConsCommonEndings
    AdjAthematicEndingsCommon ;
+MFN+Abl+Sg:i # ;
+MFN+Gen+Pl:-ium # ;

+Neut+Nom+Pl:-ia # ;
+Neut+Voc+Pl:-ia # ;
+Neut+Acc+Pl:-ia # ;

! Participles and other deverbal nominals.
LEXICON Participles
amo+Verb:aman PresStemPart ;
amo+Verb:amat SupinePart;
hortor+Verb+Dep:hortan PresStemPart ;
hortor+Verb+Dep:hortat SupinePart ;

moneo+Verb:monen PresStemPart ;
moneo+Verb:monit SupinePart ;
vereor+Verb+Dep:veren PresStemPart ;
vereor+Verb+Dep:verit SupinePart ;

rego+Verb:regen PresStemPart ;
rego+Verb:rect SupinePart ;
sequor+Verb+Dep:sequen PresStemPart ;
sequor+Verb+Dep:secut SupinePart ;
capio+Verb:capien PresStemPart ;
capio+Verb:capt SupinePart ;
patior+Verb+Dep:patien PresStemPart ;
patior+Verb+Dep:pass SupinePart ;

audio+Verb:audien PresStemPart ;
audio+Verb:audit SupinePart ;
partior+Verb+Dep:partien PresStemPart ;
partior+Verb+Dep:partit SupinePart ;

sum+Verb+Part+Fut:futur AdjThematicEndings ;

LEXICON PresStemPart
+Part+Pres:0 PresPart ;
+Ger:d Gerund ;
+Gerv:d AdjThematicEndings ;

LEXICON PresPart
+MFN+Nom+Sg:s # ;
+MFN+Voc+Sg:s # ;
+Neut+Acc+Sg:s # ;
0:t AdjCons1Pos ;
+MFN+Abl+Sg:t-e # ;

LEXICON Gerund
+Acc:-um # ;
+Gen:-i # ;
+Dat:-o # ;
+Abl:-o # ;

LEXICON SupinePart
+Part+Perf:0 AdjThematicEndings ;
+Part+Fut:ur AdjThematicEndings ;
+SupI:-um # ;
+SupII:-u # ;
