Multichar_Symbols
    +Verb
    +1S +2S +3S +1P +2P +3P ! Persons
    +Pres +Fut +Impf ! Tempora
    +Ind +Impv +Subj ! Modi
    +Act +Pass ! Voices

! Verb lexicon
LEXICON Root
! Different categories for each conjugation. Ultimately they may share parts,
! but I think this is a good starting point.
    Conj1 ;
    Conj2 ;
    Conj3 ;
    Conj4 ;

LEXICON Conj1
amo+Verb:ama C1 ;

LEXICON C1
    C14Pres ;
    C1PresSubj ;
    C12Fut ;
    C12Impf ;
    C14ImpfSubj ;

LEXICON C1PresSubj
+Pres+Subj:-e PresEndingsNonPres ;

LEXICON Conj2
moneo+Verb:mone C2 ;

LEXICON C2
    C14Pres ;
    C24PresSubj ;
    C12Fut ;
    C12Impf ;
    C14ImpfSubj ;

LEXICON Conj3
rego+Verb:reg C3 ;
capio+Verb:capI C3 ;

LEXICON C3
    C14Pres ;
    C24PresSubj ;
    C34Fut ;
    C34Impf ;
    C14ImpfSubj ;

LEXICON Conj4
audio+Verb:audi C4 ;

LEXICON C4
    C14Pres ;
    C24PresSubj ;
    C34Fut ;
    C34Impf ;
    C14ImpfSubj ;

! Regular present indicative, first to fourth conjugations.
LEXICON C14Pres
+Pres+Ind:0 PresEndingsPres ;

! Regular present subjunctive, second to fourth conjugations.
LEXICON C24PresSubj
+Pres+Subj:-a PresEndingsNonPres ;

! Future indicative, first & second conjugations.
LEXICON C12Fut
+Fut+Ind:-b PresEndingsPres ;

! Future indicative, third & fourth conjugations.
LEXICON C34Fut
+Fut+Ind:-a PresEndingsNonPres1S ;
+Fut+Ind:-e PresEndingsCommon ;

LEXICON C12Impf
+Impf+Ind:-ba PresEndingsNonPres ;

! XXX: It might be possible to do the imperfect indicative with just C12Impf
!      and insert the -e in verbs-twolc.txt. But is it worth it?
LEXICON C34Impf
+Impf+Ind:-eba PresEndingsNonPres ;

LEXICON C14ImpfSubj
+Impf+Subj:-re PresEndingsNonPres ;

! Endings for the present system, present indicative tense.
LEXICON PresEndingsPres
    PresEndingsPres1S ;
    PresEndingsCommon ;

! Endings for the present system, non-present indicative tenses.
LEXICON PresEndingsNonPres
    PresEndingsNonPres1S ;
    PresEndingsCommon ;

! Endings for the first person singular present indicative.
LEXICON PresEndingsPres1S
+1S+Act:-o # ;
+1S+Pass:-or # ;

! Endings for the first person singular of non-present indicative tenses.
LEXICON PresEndingsNonPres1S
+1S+Act:-m # ;
+1S+Pass:-r # ;

! Present system endings, common to all forms.
LEXICON PresEndingsCommon
+2S+Act:-s # ;
+3S+Act:-t # ;
+1P+Act:-mus # ;
+2P+Act:-tis # ;
+3P+Act:-nt # ;

+2S+Pass:-ris # ;
+3S+Pass:-tur # ;
+1P+Pass:-mur # ;
+2P+Pass:-mini # ;
+3P+Pass:-ntur # ;
